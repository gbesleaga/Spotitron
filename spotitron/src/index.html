<!doctype html>
<html lang="en">
<head>
  <meta charset="utf-8">
  <title>Spotitron</title>
  <base href="/">
  <meta name="viewport" content="width=device-width, initial-scale=1">
  <link rel="icon" type="image/x-icon" href="favicon.ico">

  <link rel="preconnect" href="https://fonts.gstatic.com">
  <link href="https://fonts.googleapis.com/css2?family=Lato&family=Zen+Dots&display=swap" rel="stylesheet"> 
  <!--link href="https://fonts.googleapis.com/css2?family=Roboto:wght@300;400;500&display=swap" rel="stylesheet">
  <link href="https://fonts.googleapis.com/icon?family=Material+Icons" rel="stylesheet"-->
</head>
<body>
  <script type="x-shader" id="starfield-vs">
    varying vec2 vUV;
        
    void main() {
      vUV = uv;
      gl_Position = vec4(position, 1.0);
    }
  </script>

  <script type="x-shader" id="starfield-fs">
    // TODO make adjustable and tweak for performance
		#define NUM_LAYERS 9.
    #define BRIGHTNESS .02

    uniform vec2 iResolution; // viewport resolution (pixels)
    uniform float iSpeed;   // time * factor
    uniform float iSpeedRot; // time * factor

    varying vec2 vUV;

    mat2 Rot(float a)
    {
      float s = sin(a), c = cos(a);
      return mat2(c, -s, s, c);
    }

    // makes a star at the origin
    float Star(vec2 uv, float flare)
    {
      float d = length(uv);
        
      //TWEAK
      float m = BRIGHTNESS/d;
      
      float rays = max(0., 1. - abs(uv.x*uv.y * 1000.));
      m += rays * flare;
      
      uv *= Rot(3.1415 / 4.0);
    
      rays = max(0., 1. - abs(uv.x*uv.y * 1000.));
      m += rays * .3 * flare;
      
      m *= smoothstep(1., .2, d);
      
      return m;
    }

    // return pseudo random number
    float Hash21(vec2 p)
    {
      p = fract(p * vec2(123.34, 456.21));
      p += dot(p, p + 45.32);
      
      return fract(p.x*p.y);
    }

    
    vec3 StarLayer(vec2 uv)
    {
      vec3 col = vec3(0);
      
      vec2 gv = fract(uv) - .5;
      vec2 id = floor(uv);
    
      for (int y=-1; y<=1; ++y) 
      {
        for (int x=-1; x<=1; ++x)
        {
          vec2 offs = vec2(x, y);
          float n = Hash21(id + offs); // random between 0-1
          
          float starSize = fract(n * 345.32);
          
          // flare
          float flare = smoothstep(.9, 1., starSize)* 0.02;
          
          float star = Star(gv - offs - (vec2(n, fract(n * 34.)) - .5), flare);
          
          vec3 starColor = sin(vec3(.2, .3, .9) * fract(n * 2513.4) * 16.2831) * .5 + .5;
          starColor = starColor * vec3(1., 0.25, 1. + starSize);
          
          //fade
          //star *= sin(iTime * 3. + n * 6.2831)*.5 + 1.;
          
          col += star * starSize * starColor;
        }
      }
      
      return col;
    }

		void main() {
      vec2 fragCoord = vUV * iResolution;
      vec2 uv = (fragCoord - .5*iResolution.xy)/iResolution.y;
      uv *= 0.5;

      // this controls the speed
      //float t = iTime * 0.02; // no globe speed
      //float t = iTime; // loading
      //float t = 1.8; //static;

      float t = iSpeed;

      uv *= Rot(iSpeedRot);
      
      vec3 col = vec3(0);
      
      for (float i = 0.; i < 1.; i += 1./ NUM_LAYERS)
      {
        float depth = fract(i+t);
        float scale = mix(20., .5, depth);
        float fade = depth * smoothstep(1., .9, depth);
        
        float layerOffset = 4352.256;
        col += StarLayer(uv * scale + i * layerOffset) * fade;
      }

      col = pow(col, vec3(.4545));	// gamma correction

      gl_FragColor = vec4(col,1.0);
		}
  </script>

  <script type="x-shader" id="country-vs">
    varying vec2 vUV;
    void main() {
        vUV = uv;
        vec4 p = vec4( position, 1. );

        gl_Position = projectionMatrix * modelViewMatrix * p;
    }
  </script>
    
  <script type="x-shader" id="country-fs">
    uniform sampler2D tMatCap;
    varying vec2 vUV;
    
    void main() {
        vec3 base = texture2D( tMatCap, vUV ).rgb;
        gl_FragColor = vec4( base, 1. );
    }
  </script>
  
  <app-root></app-root>
</body>
</html>
